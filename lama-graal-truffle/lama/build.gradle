plugins {
    id 'java'
    id 'antlr'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.5'
}


group 'ru.mkn.lama'
version '1.0'

repositories {
    mavenCentral()
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-no-listener", "-long-messages", "-package", "ru.mkn.lama.parser"]
}

ext {
    graal_version = '22.3.0'
    antlr_version = '4.12.0'
}

dependencies {
    implementation "org.graalvm.truffle:truffle-api:$graal_version"
    annotationProcessor "org.graalvm.truffle:truffle-dsl-processor:$graal_version"

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.3"
    antlr "org.antlr:antlr4:$antlr_version"
}

application {
    applicationDefaultJvmArgs = ['-Xss8m']
    mainClass = "ru.mkn.lama.LamaMain"
}

graalvmNative {
    binaries.all {
        buildArgs.add('--initialize-at-build-time=ru.mkn.lama,org.antlr.v4')
        buildArgs.add('-H:+AllowDeprecatedBuilderClassesOnImageClasspath')
    }
    binaries.main {
        imageName = 'lama'
        mainClass = 'ru.mkn.lama.LamaMain'
        buildArgs.add('-O2') // enables additional compiler optimizations
    }
}

test {
    useJUnitPlatform()

    jvmArgs '-ea',
            '-Dgraalvm.locatorDisabled=true',
            '-Dgraal.Dump=:1',
            '-Dgraal.PrintGraph=Network'

    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}
